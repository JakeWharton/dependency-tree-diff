import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
	alias(libs.plugins.kotlin.jvm)
	alias(libs.plugins.spotless)
}

kotlin {
	jvmToolchain(21)
}

tasks.withType(KotlinCompile).configureEach {
	compilerOptions {
		jvmTarget = JvmTarget.JVM_11
	}
}

tasks.withType(JavaCompile).configureEach {
	options.release.set(11)
}

spotless {
	kotlin {
		ktlint().editorConfigOverride([
			'ktlint_standard_filename': 'disabled',
		])
		target('**/*.kt')
	}
}

configurations {
	r8
}

dependencies {
	testImplementation(platform(libs.junit.bom))
	testImplementation libs.junit
	testImplementation libs.truth

	testRuntimeOnly(libs.junit.platform.launcher)
	testRuntimeOnly(libs.junit.vintage.engine)

	r8 libs.r8
}

def fatJarProvider = tasks.register('fatJar', Jar) { task ->
	task.dependsOn(configurations.named('runtimeClasspath'))
	task.dependsOn(tasks.named('jar'))

	task.archiveClassifier.set('fat')

	task.manifest {
		attributes 'Main-Class': 'com.jakewharton.gradle.dependencies.DependencyTreeDiff'
	}

	def sourceClasses = sourceSets.main.output.classesDirs
	task.inputs.files(sourceClasses)
	task.from files(sourceClasses)
	task.from configurations.named('runtimeClasspath').map {
		it.asFileTree.files.collect { it.isDirectory() ? it : zipTree(it) }
	}

	task.exclude '**/*.kotlin_metadata'
	task.exclude '**/*.kotlin_module'
	task.exclude '**/*.kotlin_builtins'
	task.exclude '**/module-info.class'
	task.exclude 'META-INF/maven/**'
}

def r8File = layout.buildDirectory.file("libs/${base.archivesName.get()}-r8.jar").get().asFile
def rulesFile = project.file("src/main/rules.pro")

def r8Jar = tasks.register('r8Jar', JavaExec) { task ->
	task.javaLauncher.set(javaToolchains.launcherFor { spec ->
		spec.languageVersion.set(JavaLanguageVersion.of(17))
	})
	def fatJarFile = fatJarProvider.get().archiveFile
	task.inputs.file(fatJarFile)
	task.inputs.file(rulesFile)
	task.outputs.file(r8File)

	task.classpath(configurations.r8)
	task.mainClass.set('com.android.tools.r8.R8')
	task.args = [
		'--release',
		'--classfile',
		'--output', r8File.path,
		'--pg-conf', rulesFile.path,
		'--lib', System.properties['java.home'].toString(),
		fatJarFile.get().asFile.path
	]
}

def binaryFile = layout.buildDirectory.file("${base.archivesName.get()}.jar").get().asFile
def binaryJar = tasks.register('binaryJar') { task ->
	task.dependsOn(r8Jar)

	task.inputs.file(r8File)
	task.outputs.file(binaryFile)

	task.doLast {
		binaryFile.getParentFile().mkdirs()
		binaryFile.delete()
		binaryFile << "#!/bin/sh\n\nexec java \$JAVA_OPTS -jar \$0 \"\$@\"\n\n"
		r8File.withInputStream { binaryFile.append it }

		binaryFile.setExecutable true, false
	}
}

tasks.named('assemble').configure { task ->
	task.dependsOn(binaryJar)
}

[11, 17].forEach { majorVersion ->
	def jdkTest = tasks.register("testJdk$majorVersion", Test) {
		javaLauncher = javaToolchains.launcherFor {
			languageVersion = JavaLanguageVersion.of(majorVersion)
		}

		description = "Runs the test suite on JDK $majorVersion"
		group = LifecycleBasePlugin.VERIFICATION_GROUP

		def testTask = tasks.named("test").get()
		classpath = testTask.classpath
		testClassesDirs = testTask.testClassesDirs
	}
	tasks.named("check").configure { dependsOn(jdkTest) }
}

artifacts {
	archives file: binaryFile, name: 'binary', type: 'jar', builtBy: binaryJar, classifier: 'binary'
}
